// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.19;

import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/access/AccessControl.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "@openzeppelin/contracts/utils/Base64.sol";

import "../interfaces/gameNfts/IJobNft.sol";

contract JobNft is ERC1155, AccessControl, IJobNft {
    uint256 NFT_ID_PREFIX = 10**4;

    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant DEVELOPER_ROLE = keccak256("DEVELOPER_ROLE");

    string private baseMetadataURIPrefix;
    string private baseMetadataURISuffix;
    uint256 private currentNftId;

    mapping (address => uint256) private nftId; // NFT Address => NFT ID
    mapping (uint256 => Job) private job; // tokenId => Job Status

    //*********************************************
    //Initializer
    //*********************************************
    constructor(string memory uriPrefic_, string memory uriSuffix_) ERC1155("") {
        _grantRole(DEVELOPER_ROLE, msg.sender);
        currentNftId = 2000;
        setBaseMetadataURI(uriPrefic_, uriSuffix_);
    }

    //*********************************************
    //Getter
    //*********************************************
    function getJobSeed(uint256 tokenId_) public view returns(uint256){
        return job[tokenId_].seed;
    }

    function getjobName(uint256 tokenId_) public view returns(string memory){
        return job[tokenId_].name;
    }

    function getJobType(uint256 tokenId_) public view returns(uint256){
        return job[tokenId_].jobType;
    }
    
    function uri(uint256 tokenId_) public view override returns (string memory) {
        string memory _output;
        string memory _c = ', ';
        string memory _attributes = string(abi.encodePacked(
            '[', 
            _attribute("Seed", job[tokenId_].seed), _c, 
            _attribute("Name", job[tokenId_].name), _c,
            _attribute("Type", job[tokenId_].jobType), _c,
            _attribute("Loot NFT Token Address", _getAddressAsString(job[tokenId_].rAddress)), _c,
            _attribute("Loot NFT Token ID", job[tokenId_].rTokenId),
            ']'
        ));
        string memory _image = string(abi.encodePacked(
            baseMetadataURIPrefix,
            Strings.toString(job[tokenId_].jobType),
            baseMetadataURISuffix
        ));
        string memory _json = Base64.encode(bytes(string(abi.encodePacked(
            '{"name": "Job NFT #', Strings.toString(tokenId_) ,
            '", "description": "Minted by a NFT' ,
            '", "attributes": ', 
            _attributes ,
            ', "image": "', 
            _image, 
            '"}'
        ))));
        _output = string(abi.encodePacked('data:application/json;base64,', _json));

        return _output;
    }

    function name() public pure returns (string memory){
        return 'LootAdventure JobNft';
    }

    //*********************************************
    //Setter
    //*********************************************
    function setBaseMetadataURI(string memory uriPrefix_, string memory uriSuffix_) public { 
        baseMetadataURIPrefix = uriPrefix_;
        baseMetadataURISuffix = uriSuffix_;
    }

    function setMinterRole(address granted_) public onlyRole(DEVELOPER_ROLE){
        _grantRole(MINTER_ROLE, granted_);
    }

    function setDeveloperRole(address granted_) public onlyRole(DEVELOPER_ROLE){
        _grantRole(DEVELOPER_ROLE, granted_);
    }

    function setNftId (address nft_) public onlyRole(DEVELOPER_ROLE) {
        nftId[nft_] = currentNftId;
        currentNftId++;
    }

    //*********************************************
    //Logic
    //*********************************************
    function mint(
        address to_,
        address nft_,
        uint256 tokenId_, 
        uint256 seed_,
        string memory name_,
        uint256 type_
    ) public onlyRole(MINTER_ROLE) {
        Job memory _job;
        uint256 _tokenId = (nftId[nft_] * NFT_ID_PREFIX) + type_;
        _job.seed = seed_;
        _job.name = name_;
        _job.jobType = type_;
        _job.rAddress = nft_;
        _job.rTokenId = tokenId_;

        job[_tokenId] = _job;
        _mint(to_, _tokenId, 1, "");
    }

    function _attribute(string memory traitType_, string memory value_) internal pure returns (string memory) {
        return string(abi.encodePacked('{"trait_type": "', traitType_, '", "value": "', value_, '"}'));
    }

    function _attribute(string memory traitType_, uint256 value_) internal pure returns (string memory) {
        return string(abi.encodePacked('{"trait_type": "', traitType_, '", "value": ', Strings.toString(value_), '}'));
    }

    function _attribute(string memory traitType_, address value_) internal pure returns (string memory) {
        return string(abi.encodePacked('{"trait_type": "', traitType_, '", "value": ', value_, '}'));
    }

    function _getAddressAsString(address address_) internal pure returns (string memory) {
        bytes memory _address = abi.encodePacked(address_); // アドレスをバイト列に変換
        bytes memory _hexChars = "0123456789abcdef"; // 16進数文字
        bytes memory _str = new bytes(2 + _address.length * 2); // 文字列用のバイト列

        _str[0] = "0";
        _str[1] = "x";

        // 16進数文字に変換
        for (uint256 i = 0; i < _address.length; i++) {
            _str[2 + i * 2] = _hexChars[uint8(_address[i] >> 4)];
            _str[3 + i * 2] = _hexChars[uint8(_address[i] & 0x0f)];
        }

        return string(_str);
    }

    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC1155, AccessControl) returns (bool) {
        return super.supportsInterface(interfaceId);
    }

    function _beforeTokenTransfer(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory amounts,
        bytes memory data
    )
        internal virtual override(ERC1155)
    {
        require(from == address(0), "Job-Nft Error: Token is SOUL BOUND");
        super._beforeTokenTransfer(operator, from, to, ids, amounts, data);
    }
}